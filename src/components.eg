
require:
   /dom
   .util -> css
   jquery as jq


provide:
   Custom
   CustomNoShadow
   TabData
   install-components


globals:
   document
   Text
   HTMLElement
   custom-elements


jsclass Custom < HTMLElement:

   attached-callback() =
      if not @shadow:
         @shadow = @attach-shadow with {mode = .open}
         @setup()
         @shadow.append-child(dom(css(@css())))
         Array! tpl = @template()
         tpl each t ->
            @shadow.append-child(dom(t))

   setup() =
      pass

   template() =
      div %

   css() =
      {=}


jsclass CustomNoShadow < HTMLElement:

   attached-callback() =
      if not @has-setup:
         @has-setup = true
         @setup()
         children = @child-nodes each
            Text? t -> t.text-content
            x -> x
         jq(@).empty()
         Array! tpl = @template(children)
         tpl each t ->
            super.append-child(dom(t))
         super.append-child(dom(css(@css())))

   setup() =
      pass

   template(_) =
      div %

   css() =
      {=}


class TabData:
   constructor(@tabbed-view) =
      pass

   activate() =
      @tab.class-list.add(.active)
      @pane.class-list.add(.active)
      @active = true
      @tabbed-view.active.push(@)

   deactivate() =
      @tab.class-list.remove(.active)
      @pane.class-list.remove(.active)
      @active = false
      @tabbed-view.active =
         @tabbed-view.active each a when a !== [@] -> a


jsclass ChannelTabs < Custom:
   setup() =
      @anchor = @get-attribute('anchor') or 'top'
      @active = {}
      @nchannels = 0

   template() =
      anchor-tag = '.tabs-{@anchor}'
      @element-select = dom with div.tabs-select.hide %
      @element-panes = dom with div.tabs-panes %
      if @anchor is .left or @anchor is .top:
         div.tabs-area[^anchor-tag] %
            @element-select
            @element-panes
      else:
         div.tabs-area[^anchor-tag] %
            @element-panes
            @element-select

   make-tab(spec) =
      dom with
         div.tab %
            spec.tab-elem
            onclick(e) =
               @select(spec, e.shift-key)

   add(tab-elem, pane-elem) =
      spec = TabData(@)  ;; {= tab-elem, = pane-elem}
      spec.tab-elem = tab-elem
      spec.pane-elem = pane-elem
      spec.tab = @make-tab(spec)
      spec.pane = dom(div % pane-elem)
      @element-select.append-child(spec.tab)
      @element-panes.append-child(spec.pane)
      if @active.length == 0:
         @select(spec)
      @nchannels += 1
      if @nchannels > 1 or tab-elem != '_':
         @element-select.class-list.remove(.hide)

   select(spec, add-to-active = false) =
      if @active.length > 0 and not add-to-active:
         {* @active} each a ->
            a.deactivate()
      if add-to-active and spec.active:
         spec.deactivate()
      else:
         spec.activate()

   css() =
      tab-loc =
         if @anchor is 'left' or @anchor is 'right': {
            ".tabs-area " = {
               flex-direction = .row
            }
            ".tabs-select" = {
              width = "auto"
              height = "100%"
              background = "#88f"
              display = "flex"
              flex-direction = "column"
              overflow = "auto"
            }
         }
         else: {
            ".tabs-area " = {
               flex-direction = .column
            }
            ".tabs-select" = {
              width = "100%"
              height = "auto"
              background = "#aaf"
              display = "flex"
              flex-direction = "row"
              overflow = "auto"
            }
         }

      @basic-css() & tab-loc

   basic-css() = {
      ".tabs-area" = {
         width = "100%"
         height = "100%"
         display = "flex"
         flex-direction = "column"
      }
      ;; ".tabs-area.tabs-top" = {
      ;;   flex-direction = "column"
      ;; }

      ;; ".tabs-area.tabs-left" = {
      ;;   flex-direction = "row"
      ;; }

      ;; ".tabs-top > .tabs-select.hide" = {
      ;;   display = "none"
      ;; }

      ;; ".tabs-left > .tabs-select.hide" = {
      ;;   display = "none"
      ;; }

      ".tabs-select.hide" = {
        display = "none"
      }

      ;; ".tabs-top > .tabs-select" = {
      ;;   width = "100%"
      ;;   height = "auto"
      ;;   background = "#aaf"
      ;;   display = "flex"
      ;;   flex-direction = "row"
      ;;   overflow = "auto"
      ;; }

      ;; ".tabs-left > .tabs-select" = {
      ;;   width = "auto"
      ;;   height = "100%"
      ;;   background = "#88f"
      ;;   display = "flex"
      ;;   flex-direction = "column"
      ;;   overflow = "auto"
      ;; }

      ".tabs-panes" = {
        display = .flex
        flex-direction =
           if @anchor in {.left, .right}:
              then: .column
              else: .row
        flex = "1"
        overflow = "auto"
        background =
           if @anchor in {.left, .right}:
              then: '#88f'
              else: '#aaf'
        ;; margin = '0px'
        padding = '1px'
      }

      ".tab" = {
        cursor = "pointer"
        padding-left = "5px"
        padding-right = "5px"
        padding-top = "2px"
        padding-bottom = "2px"
        user-select = "none"
      }

      ;; ".tabs-panes > *" = {
      ;;   flex = '1'
      ;;   display = "none"
      ;;   height = "100%"
      ;;   border-style = 'solid'
      ;;   border-color = 'black'
      ;;   border-width = '0px'
      ;;   border-width =
      ;;      if @anchor in {.left, .right}:
      ;;         then: '0px 0px 1px 0px'
      ;;         else: '0px 1px 0px 0px'
      ;; }

      ".tabs-panes > *" = {
        flex = '1'
        display = "none"
        height = "100%"
        ;; margin = '1px'
        background = 'white'
      }

      ".tabs-panes > .active" = {
        display = "block"
      }

      ".tab.active" = {
        background = "white"
      }
   }


jsclass ChannelLog < Custom:

   setup() =
      @is-setup = true
      @scrolled-down = true
      @has-input = @get-attribute('input') == "true"

   template() =
      @scrolled-down = true
      @area = dom with
         div.log-area %
            onscroll(e) =
               a = @jqarea
               @scrolled-down =
                  a[0].scroll-height - a.scroll-top() == a.outer-height()
            @child-nodes each x -> x
      @overlay-area = dom with
         div.overlay-area %
      @jqarea = jq(@area)
      @input = dom with
         input.log-input %
            onkeydown(e) =
               if e.key == 'Enter':
                  @onsubmit(e)
      if @has-input:
         {@overlay-area, @area, @input}
      else:
         {@overlay-area, @area}

   css() = {
      ":host" = {
         display = .flex
         flex-direction = .column
         height = "100%"
         position = 'relative'
      }

      ".log-area" = {
         flex = 1
         overflow = .auto
         margin-left = '5px'
      }

      ".overlay-area" = {
         position = 'absolute'
         right = '0px'
         top = '0px'
         background = '#eee'
         padding = '5px'
         z-index = '100'
      }

      ".overlay-area:empty" = {
         display = 'none'
      }

      ".log-input" = {
         background = '#eee'
         padding = '5px'
         font-size = '14px'
         border = 'none'
         outline = 'none'
      }

      ".log-area > *" = {
         padding-left = '20px'
         margin-left = '5px'
      }

      ".log-area > * > *" = {
         display = "inline-block"
      }

      "style" = {
         display = "none !IMPORTANT"
      }

      ".log-area > .echo" = {
         border-left = '15px solid green'
         margin-left = '0px'
         padding-left = '15px'
         background-color = '#eee'
      }

      ".log-area > .log" = {
         padding-left = '20px'
      }

      ".log-area > .result" = {
         border-left = '5px solid blue'
      }

      ".log-area > .error" = {
         border-left = '5px solid red'
      }

      ".log-area > .warning" = {
         border-left = '5px solid orange'
      }

      ".raw-line" = {
         font-family = 'monospace'
         white-space = 'pre'
         padding = '0px'
         margin = '0px'
         line-height = '1'
      }
   }

   add(child, kind = .log) =
      wrapped-child = dom(div[^'.{kind}'] % child)
      if @area:
         @area.append-child(wrapped-child)
         if @scrolled-down:
            set-timeout(f, 0) where f() =
               @jqarea.scroll-top(@area.scroll-height)
      else:
         super.append-child(wrapped-child)

   append-child(child) =
      @add(child)


install-components() =
   document.register-element('channel-log', ChannelLog)
   document.register-element('channel-tabs', ChannelTabs)

