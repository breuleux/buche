
require:
   fs
   path
   "./format" -> format
   .util -> format-buche-exception
   /dom


provide:
   extract-config
   BucheElement
   custom-elements
   make-nodes
   element-to-command


globals:
   HTMLElement, Text, custom-elements, CustomElementRegistry, Event
   Function


adjust-attributes(clone! attrs) =
   items(attrs) each {k, match v} ->
      === true ->
         attrs[k] = ""
      === false ->
         delete attrs[k]
      else ->
         pass
   attrs


make-nodes(match message, fmt) =
   undefined? or null? ->
      {}
   String? or ENode? or HTMLElement? ->
      Array! format(fmt, message)
   Array? ->
      (message each x -> make-nodes(x, fmt)).reduce(++)
   {=> tag} ->
      new-children = make-nodes(message.content, message.format or .html)
      attrs = adjust-attributes(message.attributes or {=})
      enode = ENode(tag, attrs, new-children)
      {dom(enode)}
   {=> src} ->
      resolved = path.resolve(src)
      content = fs.read-file-sync(resolved, .utf8)
      ext = path.extname(resolved).slice(1)
      fmt-obj = match message.format:
         undefined? ->
            {name = ext}
         String? s ->
            {name = s}
         obj ->
            obj
      make-nodes(content, fmt-obj & {directory = path.dirname(resolved)})
   {=> content} ->
      make-nodes(content, message.format or .html)
   else ->
      throw Error('Cannot make a node out of this.')


method HTMLElement.prototype.dispatch(path, cmd, data) =
   m = @['command_{cmd}']
   if m:
      m.call(@, path, cmd, data)
   else:
      e = new Error('Unrecognized command: {cmd}')
      e.json-message = {parent = path, command = cmd, data = data}
      throw e


method HTMLElement.prototype.command_log(path, _, data) =
   make-nodes(data) each
      HTMLElement? node ->
         match node.tag-name.to-lower-case():
            R"^command-([a-z]+)$"! {_, command-name} ->
               dat = element-to-command(node, command-name)
               @dispatch(path, dat.command, dat.data)
            "script" and match is node.get-attribute('type') ->
               R"^buche/([a-z]+)$"! {_, command-name} ->
                  dat = JSON.parse(node.text-content)
                  @dispatch(path, command-name, dat)
               else ->
                  @append-child(node)
            else ->
               @append-child(node)
      node ->
         @append-child(node)


method HTMLElement.prototype.command_clear(_, _, data) =
   @innerHTML = ""


method HTMLElement.prototype.command_eval(_, _, data) =
   fn = Function(data.expression)
   fn.call(@)


method HTMLElement.prototype.buche-dispatcher() =
   var root = [@]
   while root and not root._buche-dispatcher:
      root = root.parent-node
   root and root._buche-dispatcher


method HTMLElement.prototype.buche-send(obj, arg) =
   message = match obj:
      Event? ->
         obj.stop-propagation()
         {
            event-type = obj.type
            modifiers = {
               ctrl = obj.ctrl-key
               shift = obj.shift-key
               alt = obj.alt-key
               meta = obj.meta-key
            }
            address = @get-attribute(.address)
            which = obj.which
         }
      Number? or String? ->
         {
            argument = obj
         }
      else ->
         obj

   if typeof(arg) is .object:
      Object.assign(message, arg)
   else:
      message.argument = arg

   message.path or= @buche-path or null

   var curr = [@]
   while curr and not curr.buche-path and curr.parent-node:
      curr = curr.parent-node

   message.enclosing-path = curr??.buche-path

   @buche-dispatcher().source.send(message)


element-to-command(node, command) =
   data = {=}

   node.attributes each attr ->
      data[attr.name] = match attr.value:
         "" -> true
         else ->
            try:
               JSON.parse(attr.value)
            catch err:
               attr.value

   data.children = node.child-nodes

   return {=command, =data}


extract-config(@) =
   spec = @attribute-spec()
   config = @attribute-defaults()
   @attributes each attr ->
      name = attr.name
      v =
         if spec[name]:
            spec[name](attr.value)
         else:
            attr.value
      config[name] = v
   items(adjust-attributes(config)) each {name, v} ->
      @set-attribute(name, v)
   config


jsclass BucheElement < HTMLElement:

   constructor() =
      super()
      @_locked = false

   attribute-defaults() =
      {=}

   attribute-spec() =
      {=}

   _lock() =
      if not @_locked:
         @_locked = true
         config = extract-config(@)
         commands = {}
         children = @child-nodes each
            HTMLElement? node ->
               match node.tag-name.to-lower-case():
                  R"^command-([a-z]+)$"! {_, command-name} ->
                     dat = element-to-command(node, command-name)
                     commands.push with {dat.command, dat.data}
                  "script" and match is node.get-attribute('type') ->
                     R"^buche/([a-z]+)$"! {_, command-name} ->
                        dat = JSON.parse(node.text-content)
                        commands.push with {command-name, dat}
                     else ->
                        node
                  else ->
                     node
            other -> other
         @innerHTML = ""
         @setup(config, children)
         commands each {cmd, dat} ->
            @dispatch(@buche-path, cmd, dat)

   setup(config, children) =
      @config = config
      children each ch ->
         @push-child(ch)

   connected-callback() =
      try:
         @_lock()
      catch err:
         m = err.context or 'No message info.'
         match @buche-dispatcher():
            undefined? or null? ->
               throw err
            d ->
               d._dispatch(format-buche-exception(m, err))


   append-child(ch) =
      if @_locked:
         @push-child(ch)
      else:
         super.append-child(ch)

   _append-child(ch) =
      super.append-child(ch)


jsclass PlaceholderElement < HTMLElement:
   constructor() =
      super()

   connected-callback() =
      tag = @get-attribute(.tag)
      @set-attribute(.title) with
         'Undefined tag: <{tag}>. You may need to install a plugin.'
      @append-child(dom('<{tag}>'))


_standard-hyphenated = {
   "annotation-xml" = true
   "color-profile" = true
   "font-face" = true
   "font-face-src" = true
   "font-face-uri" = true
   "font-face-format" = true
   "font-face-name" = true
   "missing-glyph" = true
}


CustomElementRegistry.prototype.is-valid-name(name) =
   ;; This is not exhaustive. Actual spec at:
   ;; https://www.w3.org/TR/custom-elements/#valid-custom-element-name
   R"[a-z_]+-[a-z_]+"? name and not _standard-hyphenated[name]


elements-map = {
   "placeholder-element" = PlaceholderElement
}


items(elements-map) each {tag, cls} ->
   custom-elements.define(tag, cls)
