
require:
   node-uuid as uuid
   events -> EventEmitter
   /dom

provide:
   channel-constructors


class SubChannels:

   constructor(@default-type, @install) =
      @channels = {=}

   parse(m) =
      if m.path is '/':
         return {false, m}
      {_, sub-path, *rest} = m.path.split('/')
      sub-m = m & {path = '/' + rest.join('/')}
      return {sub-path, sub-m}

   get(name, message) =
      if @channels[name] as sch:
         sch
      else:
         type = message.type or @default-type
         expr-value @channels[name] =
            channel-constructors[type](message, @install)


class LogChannel < EventEmitter:

   constructor(message, install) =
      @area = dom(div.log-area %)
      install(@area)
      @sub-channels = SubChannels(.log) with elem ->
         @area.append-child(elem)

   dispatch(m) =
      {sub-path, sub-m} = @sub-channels.parse(m)
      if sub-path:
         ch = @sub-channels.get(sub-path, sub-m)
         if sub-m.command !== .open:
            ch.dispatch(sub-m)
      elif m.command is .log:
         element =
            match m.format:
               .pre ->
                  div.raw-line % m.contents
               .html ->
                  div[raw] % m.contents
               .markdown ->
                  require: marked
                  div[raw] % marked.inline-lexer(m.contents, {})
               .quaint ->
                  require: quaint
                  div[raw] % quaint.toHTML(m.contents)
               else ->
                  div % m.contents
         @area.append-child(dom(element))
      elif m.command is .clear:
         while @area.first-child:
            @area.remove-child(@area.first-child)
      else:
         console.error('log channel does not know command {m.command}.')

channel-constructors = {
   log = LogChannel
   error = LogChannel
}
