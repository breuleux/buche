
require:
   events -> EventEmitter
   electron -> ipc-renderer
   /browser -> document
   .elements ->
      BucheElement as _BucheElement
   .source ->
      StreamSource
   .dispatch ->
      RootDispatcher
   .registry ->
      PluginManager
   buche-tools


globals:
   Buche, BucheElement


BucheElement = _BucheElement
Buche = buche-tools


plugins = PluginManager('~/.config/buche/')
{
   'code-snippet'
   'buche-log'
   'buche-tabs'
   'buche-input'
   'buche-null'
} each plugin-name ->
   p = require: ^['./plugins/{plugin-name}']
   plugins.import(p)


stdin-buffer = new EventEmitter()
ipc-renderer.on(.data) with {event, data, origin} ->
   stdin-buffer.emit(.data, data, origin)
ipc-renderer.on(.end) with {event, data} ->
   stdin-buffer.emit(.end)
ipc-renderer.send(.ready)


main() where main() =
   mount-content = document.get-element-by-id("buche-mount-content")
   stream =
      StreamSource with
         stdin-buffer
         with write(x) =
            ipc-renderer.send(.data, x)
   dispatcher = RootDispatcher(stream, mount-content, plugins)
   document.dispatcher = dispatcher
   dispatcher.run()
