
require:
   /dom
   marked

provide:
   format

globals:
   document


fix-script-tags(node) =
   ;; <script> tags creates through setting innerHTML won't run.
   ;; This method fixes that behavior by replacing all script tags
   ;; by explicitly built nodes.
   node.query-selector-all('script') each s ->
      s2 = document.create-element('script')
      s2.type = s.type
      if s.src:
         s2.src = s.src
      s2.text-content = s.text-content
      s.parent-node.replace-child(s2, s)


extract-raw(text) =
   dummy = document.create-element('div')
   dummy.innerHTML = text
   fix-script-tags(dummy)
   match consume(dummy.child-nodes):
      {x} ->
         x
      xs ->
         xs


formats = {

   direct(node, options) =
      node

   enode(enode, options) =
      dom(enode)

   text(text, options) =
      document.create-text-node(text)

   stdout(text, options) =
      dom with
         span.stdout % text

   markdown(text, options) =
      rval = document.create-element('div')
      rval.innerHTML = marked(text)
      rval

   html(text, options) =
      extract-raw(text)

   ;; Aliases

   md = .markdown
   txt = .text
}


format(fmt, obj) =
   {name, options} = match fmt:
      String? ->
         {fmt, {name = fmt}}
      {=> name} ->
         {name, fmt}
      else ->
         throw new Error with
            'Cannot parse format: "{fmt}"'

   var formatter = formats[name]
   while String? formatter:
      formatter = formats[formatter]
   if not formatter:
      throw new Error with
         'Unknown format: "{name}"'
   return formatter(obj, options)
