
require:
   /dom

provide:
   format

globals:
   document


extract-raw(text) =
   dummy = document.create-element('div')
   dummy.innerHTML = text
   match consume(dummy.child-nodes):
      {x} ->
         x
      xs ->
         xs


formats = {

   direct(node, options) =
      node

   enode(enode, options) =
      dom(enode)

   text(text, options) =
      document.create-text-node(text)

   pre(text, options) =
      dom with
         div.raw-line % text

   html(text, options) =
      extract-raw(text)

}


format(fmt, obj) =
   {name, options} = match fmt:
      String? ->
         {fmt, {name = fmt}}
      {=> name} ->
         {name, fmt}
      else ->
         throw new Error with
            'Cannot parse format: "{fmt}"'

   formatter = formats[name]
   if not formatter:
      throw new Error with
         'Unknown format: "{name}"'
   return formatter(obj, options)
