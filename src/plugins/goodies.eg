
require:
   /dom
   "highlight.js" as hljs
   fs
   ..components ->
      TabData, BucheElement
   ..format ->
      format as fmt


globals:
   Text


jsclass ReplObject < BucheElement:
   setup() =
      @onclick(e) =
         e.stop-propagation()
         ch = @get-channel()
         if ch:
            ch.write with {
               command = .click
               shift = e.shift-key
               ctrl = e.ctrl-key
               alt = e.alt-key
               obj-id = @get-attribute('obj-id')
            }


jsclass IBox < BucheElement:
   setup() =
      ch = @get-channel()
      obj-id = @get-attribute('obj-id')
      @onclick(e) =
         e.stop-propagation()
         if ch:
            ch.write with {
               command = .click
               button = match e.button:
                  0 -> .left
                  1 -> .middle
                  2 -> .right
               shift = e.shift-key
               ctrl = e.ctrl-key
               alt = e.alt-key
               meta = e.meta-key
               = obj-id
            }

      path = '{ch.path}/#{obj-id}'
      ch.master.channels[path] = [@]

   dispatch(m and {command => match}) =
      .replace ->
         node = fmt(@, m.format or .html, m.content or m.contents, m)
         @innerHTML = node.innerHTML
      .highlight ->
         if m.color is undefined:
            m.color = .yellow
         color = m.color or 'transparent'
         @style.backgroundColor = color
      .style ->
         style = match m.style:
            null? ->
               ""
            String? s ->
               s
            else ->
               throw Error('TODO: support style dictionary')
         print style
         @style.css-text = style
      else ->
         throw Error('Unknown command: {m.command}')


jsclass TabbedView < BucheElement:
   setup() =
      @anchor = @get-attribute('anchor') or 'top'
      @active = {}

   select(spec, add-to-active = false) =
      if @active.length > 0 and not add-to-active:
         {* @active} each a ->
            a.deactivate()
      if add-to-active and spec.active:
         spec.deactivate()
      else:
         spec.activate()

   eg-template(children) =
      anchor-tag = '.tabs-{@anchor}'
      datas = {}
      tabs = {}
      panes = {}

      messages = {}
      rest = {}

      children each match node ->
         String? tc and match is tc ->
            R"^[ \n]*$"? ->
               pass
            else ->
               messages.push(tc)
         {tag-name => match} ->
            .VIEW ->
               data = TabData(@)
               {data.tab, data.pane} = node.child-nodes each x when not Text? x -> x
               sel(e) = @select(data, e.shift-key)
               data.tab.query-selector-all('*') each child ->
                  ;; Disable children's existing click events.
                  child.onclick = sel
               data.tab.onclick = sel
               datas.push(data)
               tabs.push(data.tab)
               panes.push(data.pane)
               if node.get-attribute('active') !== null:
                  data.activate()
            .STYLE or .LINK ->
               rest.push(node)
            else ->
               messages.push(node)

      if @active.length == 0:
         datas[0]??.activate()

      div.tabbed-view-holder[^anchor-tag] %
         rest
         div.tabbed-view-tabs %
            tabs
         if messages.length > 0:
            then:
               div.tabs-global %
                  messages
            else:
               ""
         div.tabbed-view-panes %
            panes

   css() = {

      "tabbed-view" = {
         display = "inline-block"
      }

      ".tabbed-view-holder" = {
         display = "flex"
         flex-direction = "column"  ;; use anchor
      }

      ".tabbed-view-tabs" = {
         display = "flex"
         flex-direction = "row"
         flex-wrap = "wrap"
         overflow = "auto"
         border = "1px solid black"
      }

      ".tabbed-view-holder tab" = {
         display = "inline-block"
         cursor = "pointer"
         margin = "2px 3px 2px 3px"
         padding = "0px 2px 0px 2px"
         border = "1px solid black"
         user-select = "none"
         ;; background = "yellow"
      }

      ".tabbed-view-holder tab.active" = {
         background = "white"
         border = "1px solid white"
      }

      ".tabs-global" = {
         border = "1px solid black"
         padding = "2px"
         border-top = "none"
      }

      ".tabbed-view-panes" = {
         display = "flex"
         flex-direction = "row"
         flex = "1"
         overflow = "auto"
      }

      ".tabbed-view-panes > *" = {
         display = "none"
         padding = "2px"
         flex = "1"
         border = "1px solid black"
         border-top = "none"
      }

      ".tabbed-view-panes > .active" = {
         display = "block"
      }
   }


jsclass CodeSnippet < BucheElement:
   setup() =
      @filename = @get-attribute('src')
      try:
         @contents = fs.read-file-sync(@filename, .utf8)
      catch e:
         @contents = match @child-nodes[0]:
            undefined? -> ""
            Text? t -> t.text-content
            node -> node.inner-text
      @lang = @get-attribute('language') or 'auto'
      Number! @column = @get-attribute('column') or -1
      @column -= 1
      Number! @lineno = @get-attribute('line') or 1
      @lineno -= 1
      Number! @context = @get-attribute('context') or 0
      lines = @contents.split(R"\n") each line -> '{line}\n'
      lo = Math.max(@lineno - @context, 0)
      hi = Math.min(@lineno + @context, lines.length - 1)
      lang-class = '.{@lang}'

      @excerpt = dom with
         div.hljs[^lang-class] %
            lines[lo ... @lineno]
            span.hljs-line-focus %
               if @column < 0:
                  then:
                     lines[@lineno]
                  else:
                     l = lines[@lineno]
                     {
                        l[0 ... @column]
                        span.hljs-column-focus % l[@column]
                        l[(@column + 1) ...]
                     }
            lines[(@lineno + 1) .. hi]
      @hl-excerpt = hljs.highlight-block(@excerpt)
      @hl-excerpt = @excerpt

      ;; @excerpt = lines[lo..hi].join('\n')
      ;; @hl-excerpt = hljs.highlight(@lang, @excerpt).value

      @line-numbers = (lo..hi each i -> ___) with
         if i == @lineno:
            then: div.hljs-lineno-focus % '{i + 1}'
            else: div % '{i + 1}'

   eg-template(children) = {
      link %
         rel = 'stylesheet'
         type = 'text/css'
         href = '../node_modules/highlight.js/styles/github.css'
      div %
         code.filename %
            @filename
         div.hljs-box %
            .hljs-linenos %
               @line-numbers
            @hl-excerpt
            ;; .hljs[raw] %
            ;;    @hl-excerpt
   }

   css() = {
      "code.filename" = {
         display = .block
         background = '#e8e8e8'
      }

      ".hljs-lineno-focus" = {
         background = '#ddd'
         color = .purple
      }
      ".hljs-line-focus" = {
         background = '#ddd'
         display = 'block'
      }
      ".hljs-column-focus" = {
         background = '#aaa'
         ;; outline = '2px solid #777'
         ;; background = 'linear-gradient(to right, #aaa, #ddd)'
      }
      ".hljs-box" = {
         display = "flex"
         flex-direction = "row"
      }
      ".hljs-linenos, .hljs" = {
         font-size = "16px"
         line-height = "1.2"
         font-family = .monospace
         white-space = .pre
      }
      ".hljs-linenos" = {
         font-weight = .bold
         padding-right = "4px"
         border-right = "2px solid purple"
      }
      ".hljs" = {
         flex = "1"
         padding = "0px !IMPORTANT"
         padding-left = "4px !IMPORTANT"
      }
   }


provide:
   {=} as channels
   {"repl-object" = ReplObject
    "i-box" = IBox
    "tabbed-view" = TabbedView
    "code-snippet" = CodeSnippet} as components
