
require:
   /dom
   fs
   buche-tools -> Channel
   ..components -> CustomShadow
   ..format -> formats


globals:
   document


jsclass TemplateChannel < Channel:

   make-element() =
      d = dom(div %)
      d.style.width = "100%"
      d.style.height = "100%"
      shadow = d.attach-shadow(mode = .open)
      contents = @options.contents or fs.read-file-sync(@options.src, .utf8)
      shadow.innerHTML = contents
      d.shadow = shadow
      d

   setup() =
      @element.shadow.query-selector-all('channel') each chelem ->
         attrs = object with chelem.attributes each attr ->
            unhyph = attr.node-name.replace(R.g"\-([a-z])") with
               {_, x} -> x.to-upper-case()
            val = if{attr.node-value is "", true, attr.node-value}
            {unhyph, val}
         attrs.path = '{@path}/{attrs.path}'
         ch = @master.construct(@, attrs.type, attrs)
         @register-channel(ch)
         chelem.parent-node.replace-child(ch.element, chelem)

   add-channel(ch) =
      pass


provide:
   true as is-buche-plugin
   {template = TemplateChannel} as channels
   {=} as components
