
require:
   /dom
   buche-tools -> Channel
   ..format -> format as fmt


jsclass TableChannel < Channel:

   setup() =
      @columns = @options.columns or null
      @data = {}
      @hidden = @options.hidden

   add-data(d) =
      if @columns is null:
         @columns = object with items(d) each {name, value} ->
            {name} with match value:
               Number? ->
                  {type = 'number'}
               String? ->
                  {type = 'string'}
               else ->
                  {type = 'string'}
      if @data.length == 0:
         row = dom with
            tr %
               items(@columns) each {colname, col} ->
                  th %
                     class = "column-type-" + col.type
                     colname
         @element.append-child(row)
      @data.push(d)
      @emit('add-data', d)
      if not @hidden:
         row = dom with
            tr %
               items(@columns) each {colname, col} ->
                  td %
                     class = "column-type-" + col.type
                     fmt(@, col.format or .text, String{d[colname]}, col, d)
         @element.append-child(row)

   make-element() =
      dom with
         if @hidden:
            then:
               div %
            else:
               table.table-channel %
                  style % """
                  table.table-channel {
                     border-collapse: collapse;
                  }
                  table.table-channel td, table.table-channel th {
                     padding-left: 5px;
                     padding-right: 5px;
                  }
                  table.table-channel tr th {
                     border-bottom: 3px solid black;
                  }
                  table.table-channel td.column-type-number {
                     text-align: right;
                  }
                  """

   dispatch_data(clone! m) =
      if m.data:
         @add-data(m.data)
      else:
         delete m.path
         delete m.command
         @add-data(m)

   open-channel(_) =
      throw Error('Cannot open a sub-channel in a TableChannel.')


provide:
   true as is-buche-plugin
   {table = TableChannel} as channels
   {=} as components
