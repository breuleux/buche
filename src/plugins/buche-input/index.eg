
globals:
   document, HTMLElement, custom-elements


require:
   path
   /dom
   jquery as jq
   ...elements -> BucheElement


provide:
   buche-install


jsclass BucheInput < BucheElement:

   setup(config, children) =
      @current = 0
      @history = {}
      if children.length > 0:
         throw Error('No children allowed for buche-input')
      @_input = dom with
         input %
            onkeydown(event and match is event.key-code) =
               code and (38 or 40) ->
                  ;; Up/Down
                  event.prevent-default()
                  @buche-send(event) with {
                     event-type = .keyup
                     which = if{code == 38, .Up, .Down}
                     value = event.target.value
                  }
               else ->
                  pass

            onkeyup(event and match is event.key-code) =
               13 ->
                  ;; Enter
                  event.prevent-default()
                  @buche-send(event) with {
                     event-type = .submit
                     which = undefined
                     value = event.target.value
                  }
               code ->
                  pass
      @_append-child(@_input)

   command_set(_, _, data) =
      @_input.value = data.value or ""

   command_append(_, _, data) =
      @_input.value += data.value or ""

   command_focus(_, _, data) =
      @_input.focus()


buche-install() =
   custom-elements.define('buche-input', BucheInput)
   document.head.append-child(dom(___)) with
      link %
         type = "text/css"
         rel = .stylesheet
         href = path.join(__dirname, "style.css")
