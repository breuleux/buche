
require:
   /dom
   buche-tools -> Channel
   ..format -> format as fmt
   ..registry ->
      DelayMessage, events
      import-failures, make-installer-component


jsclass PlaceholderChannel < Channel:

   make-element() =
      @type = @options._placeholder-type
      try-to-install =
         if import-failures[@type]:
            {@type}
         else:
            items(import-failures) each {name, _} -> name
      dom with
         div %
            style = 'width:100%;height:100%'
            div.placeholder %
               div %
                  style = 'padding:10px'
                  'Channel of type '
                  b % @options._placeholder-type
                  ' cannot be created.'
               try-to-install each name ->
                  make-installer-component(name) with
                     {"Do you wish to install ", b % name, "?"}

   setup() =
      @channel = null
      @token = Symbol('ph/{@type}')
      events.once('add-channel-type-{@type}') with {cls} ->
         @channel = new cls(@master, @options)
         @element.innerHTML = ""
         @element.append-child(@channel.element)
         @master.unblock-messages(@token)

   dispatch(m) =
      if @channel is null:
         throw DelayMessage(@token, m)
      else:
         @channel.dispatch(m)

   open-channel(options) =
      if @channel is null:
         throw DelayMessage(@token, null)
      else:
         @channel.open-channel(options)

   ;; TODO: other things to redirect?


provide:
   true as is-buche-plugin
   {placeholder = PlaceholderChannel} as channels
   {=} as components
