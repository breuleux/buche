
require:
   electron ->
      app, BrowserWindow, ipc-main
   fs
   path
   minimist
   shell-escape
   child_process -> spawn
   open
   ..package -> version
   .registry -> PluginManager
   .util -> format-buche-exception
   /html

plugins = PluginManager('~/.config/buche/')

var main-window = null
var proc = null

usage = """
Usage: buche [options] [command]

Options:

  --tee [file]         File to log the command's stdout to.
  --inspect            Print stdout and stdin.
  --install plugin     Install a buche plugin.
  --uninstall plugin   Uninstall a buche plugin.
  --dev                Open developer console.
  -h, --help           Output usage information.
  -v, --version        Output the version number.

Note:
   The options must come before the command. Any options written
   after the command are the options for that command.
"""

async parse-options(argv) =
   var help = false
   cfg = {
      boolean = {'dev', 'dump', 'inspect', 'version', 'help'}
      string = {'tee', 'install', 'uninstall', 'require'}
      alias = {
         v = .version
         h = .help
         t = .tee
         r = .require
      }
      stop-early = true
      unknown(match x) =
         R"^-"? ->
            console.error('Unknown option: {x}')
            help = true
            false
         else ->
            true
   }
   opts = minimist(argv, cfg)
   if help or opts.help:
      console.error(usage)
      null
   elif opts.version:
      print 'buche v{version}'
      null
   elif opts.install:
      await plugins.install(opts.install)
      null
   elif opts.uninstall:
      await plugins.uninstall(opts.uninstall)
      null
   else:
      opts.cmd = match opts._:
         {cmd} -> cmd
         args -> shell-escape(args)
      opts


create-window(options) =

   cmd = options.cmd
   if cmd:
      print 'Using command: {cmd}'

   var show-splash = if{cmd, false, true}

   stream =
      if options.tee:
         print 'Logging into: {options.tee}'
         fs.create-write-stream(options.tee)
      else:
         null

   w = new BrowserWindow with {
      width = 1200
      height = 900
      title = cmd or 'buche'
      web-preferences = {
         experimental-features = true
      }
   }

   w.loadURL('file://{__dirname}/../html/index.html')
   if options.dev:
      w.web-contents.open-dev-tools(mode = .bottom)
   w.on(.closed) with {} ->
      main-window = null

   handle-redirect(e, url) =
      if url != w.web-contents.getURL():
        e.preventDefault()
        open(url)

   w.web-contents.on('will-navigate', handle-redirect)
   w.web-contents.on('new-window', handle-redirect)

   web-send(data, trail = '\n', origin = "buche") =
      show-splash = false
      if stream:
         stream.write(data)
      if options.inspect:
         data.split('\n') each line when line ->
            print '-> {line}'
      try:
         w.web-contents.send(.data, data + trail, origin)
      catch e:
         console.error(e.stack)

   splash() =
      data = {
         command = .log
         parent = '/'
         tag = 'div'
         attributes = {
            style = 'margin-left: 20px; margin-right: 20px'
         }
         content = {
            src = '{__dirname}/../html/splash.md'
         }
      }
      web-send(JSON.stringify(data))

   ipc-main.on(.ready) with ->
      var origins = {}
      if cmd:
         proc = spawn(cmd, {}) with {
            shell = true
            stdio = {.pipe, .pipe, .pipe} ;; process.stderr}
         }
         proc.on(.error) with err ->
            print 'Failed to start process.'
            print err
         origins = {#stdout{proc.stdout}, #stderr{proc.stderr}}
         ipc-main.on(.data) with {_, data} ->
            if options.inspect:
               print '<- {data}'
            try:
               proc.stdin.write(data + '\n')
            catch e:
               web-send(format-buche-exception(___)) with
                  data, e
                  "A command was sent to the command's standard input,"
                  \ + " but an error occurred:"
      else:
         origins = {#stdout{process.stdin}}
         ipc-main.on(.data) with {_, data} ->
            console.error('Cannot receive data!')
            console.error(JSON.stringify(data))
      origins each {origin-name, origin} ->
         origin.set-encoding(.utf8)
         origin.on(.data) with data ->
            web-send(data, '', origin-name)
         origin.on(.end) with ->
            if show-splash:
               splash()
            try:
               w.web-contents.send(.end)
            catch e:
               console.error(e.stack)

      if show-splash and process.stdin.isTTY:
         splash()
         show-splash = false

main() where async main() =
   options = await parse-options(process.argv[cut...]) where cut =
      if path.resolve(process.argv[1]) is __filename:
         2
      else:
         1

   if options is null:
      process.exit(0)

   app.on('ready') with _ -> create-window(options)

   app.on("window-all-closed") with {} ->
      app.quit()
      if proc:
         proc.stdin.end()

   app.on(.activate) with {} ->
      if main-window === null:
         create-window(options)
